import groovy.io.FileType

repositories {
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

apply plugin: 'com.android.application'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.0"

  defaultConfig {
    applicationId "com.orhanobut.mockwebserverplus"
    minSdkVersion 16
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

task generateFixtures(group: 'fixtures') << {
  def directory = projectDir.path + '/src/test/java'
  new File(directory + '/fixtures').mkdir()
  def path = directory + "/fixtures/Fixtures.java"

  def builder = '' << ''
  builder.append("package fixtures;\n\n")
  builder.append("public class Fixtures {\n\n")
  builder.append("  private Fixtures() {\n")
  builder.append("    //no instance\n")
  builder.append("  }\n\n")

  def resources = android.sourceSets.test.resources.srcDirs.getAt(0)
  if (resources.size() > 0) {
    resources.eachDirMatch("fixtures") { dir ->
      def fixturesFile = dir
      fixturesFile.eachFile(FileType.FILES) { file ->
        if (file.name.endsWith(".yaml")) {
          String fileName = file.name.split('\\.')[0]
          builder.append("  public static final String ")
              .append(fileName.toUpperCase())
              .append(" = ")
              .append('\"')
              .append(fileName)
              .append('\";\n')
        }
      }
    }
  }
  builder.append("}\n")

  new File(path).write(builder.toString())
}

afterEvaluate {
  generateDebugSources.dependsOn generateFixtures
  generateReleaseSources.dependsOn generateFixtures
}

dependencies {
  compile 'com.android.support:appcompat-v7:25.0.0'
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:3.5.2'
  testCompile 'org.mockito:mockito-core:2.2.7'
  testCompile 'com.squareup.okhttp3:okhttp:3.4.1'

  testCompile project(':mockwebserverplus')
}

